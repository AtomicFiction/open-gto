dnl 
dnl Process this file with autoconf to produce a configure script.
dnl 

dnl A few non-standard check definitions:

AC_DEFUN([AC_CXX_NAMESPACES],
[AC_CACHE_CHECK(whether the compiler implements namespaces,
ac_cv_cxx_namespaces,
[AC_LANG_SAVE
 AC_LANG_CPLUSPLUS
 AC_TRY_COMPILE([namespace Outer { namespace Inner { int i = 0; }}],
                [using namespace Outer::Inner; return i;],
 ac_cv_cxx_namespaces=yes, ac_cv_cxx_namespaces=no)
 AC_LANG_RESTORE
])
if test "$ac_cv_cxx_namespaces" = yes; then
  AC_DEFINE(HAVE_NAMESPACES,,[define if the compiler implements namespaces])
fi
])

AC_DEFUN([AC_CXX_HAVE_STL],
[AC_CACHE_CHECK(whether the compiler supports Standard Template Library,
ac_cv_cxx_have_stl,
[AC_REQUIRE([AC_CXX_NAMESPACES])
 AC_LANG_SAVE
 AC_LANG_CPLUSPLUS
 AC_TRY_COMPILE([#include <list>
#include <deque>
#ifdef HAVE_NAMESPACES
using namespace std;
#endif],[list<int> x; x.push_back(5);
list<int>::iterator iter = x.begin(); if (iter != x.end()) ++iter; return 0;],
 ac_cv_cxx_have_stl=yes, ac_cv_cxx_have_stl=no)
 AC_LANG_RESTORE
])
if test "$ac_cv_cxx_have_stl" = yes; then
  AC_DEFINE(HAVE_STL,,[define if the compiler supports Standard Template Library])
fi
])


dnl ********************** Starts here...

AC_PREREQ(2.57)

AC_INIT(Gto, 2.0)

AC_CONFIG_AUX_DIR(config)

AM_INIT_AUTOMAKE()
AC_LANG(C++)
AC_PROG_CC
AC_PROG_CXX
AC_PROG_RANLIB
AC_PROG_LIBTOOL

AC_HEADER_STDC
AC_C_CONST
AC_TYPE_SIZE_T
AC_FUNC_STAT
AC_CHECK_HEADERS([unistd.h])

AC_CHECK_LIB(z, gzopen, [AC_DEFINE(GTO_SUPPORT_ZIP) LIBS="$LIBS -lz"])

AC_CXX_HAVE_STL
if test "$ac_cv_cxx_have_stl" != yes; then
    AC_MSG_ERROR(STL Support is required)
fi

AC_MSG_CHECKING(for istream::fail())
        AC_TRY_COMPILE(
        [
        #include <iostream>
        ],
        [
        std::istream *xxx;
        if(xxx->fail());
        ],
        [AC_MSG_RESULT(yes)
        AC_DEFINE(HAVE_FULL_IOSTREAMS)],
        [AC_MSG_RESULT(no)])


AC_CONFIG_FILES([Makefile
                 bin/Makefile
                 bin/gtoinfo/Makefile
                 lib/Gto/Makefile
                 lib/Gto/test/Makefile
                 lib/Makefile])

AC_OUTPUT
